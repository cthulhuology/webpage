#!/usr/bin/env escript
%% -*- erlang -*-
%%! -pa $(dirname 0)/ebin -noshell -noinput
%%
%% Copyright 2016 David J. Goehrig <dave@dloh.org>

-record(response, {
	socket, module, function, args, upgrade = false,
	status = 200,
	protocol = <<"HTTP/1.1">>,
	headers = [],
	body = <<>>
}).

%% starts a local node to issue commands from
%% we name these sequentially with system time
%% to avoid colliding with other command processes
connect(Server) ->
	net_kernel:start([ list_to_atom("cmd" ++ integer_to_list(erlang:system_time()) ++ "@localhost"), shortnames ]),
	case net_kernel:connect(Server) of
		false -> 	
			io:format("Failed to connect to ~p~n", [ Server ]),
			erlang:halt(0);
		true -> ok
	end.

%% process takes the command line arugments and performs
%% the associated action
process(Server,["init"]) ->
	io:format("starting ~p~n", [ Server ]),
	process_flag(trap_exit,true),
	case net_kernel:start([Server,shortnames]) of
		{ ok, Pid } ->
			io:format("Started ~p at ~p~n", [ Server, Pid ]),
			connect(Server),
			rpc:call(Server, application,load,[webpage]),
			rpc:call(Server, webpage_database, initialize,[]);
		{ error, Reason } ->
			io:format("Failed to start ~p: ~p~n", [ Server, Reason ]),
			erlang:halt(1)
	end;

process(Server, [ "setup", File ]) ->
	connect(Server),
	{ ok, Config } = file:consult(File),
	io:format("loading config ~p~n", [ Config ]),
	Routes = proplists:get_value(routes, Config),
	[ rpc:call(Server, webpage_router,add,[ Path, Module ]) ||  { Path, Module } <- Routes ],
	Files = proplists:get_value(files, Config),
	[ rpc:call(Server,webpage_file,add,[ Path, Local, Mime ])  || { Path, Local, Mime } <- Files ],
	Users = proplists:get_value(users, Config),
	[ setup_user(Server, User, Email, Pass, Paths) ||
		{ User, Email, Pass, Paths } <- Users ],
	rpc:call(Server,mnesia,sync_log,[]),
	io:format("done~n");

process(Server,["status"]) ->
	connect(Server),
	io:format("ok~n");

process(Server,["file","add", Path, Filename, Mime]) ->
	connect(Server),
	rpc:call(Server,webpage_router,add, [ Path, [[ webpage_file, []]]]),
	rpc:call(Server,webpage_file,add, [ Path, Filename, binary:list_to_bin(Mime)]);

process(Server,["file","remove", Path]) ->
	connect(Server),
	rpc:call(Server,webpage_router,remove, [ Path ]),
	rpc:call(Server,webpage_file, remove, [ Path ]);

process(Server,["file","get", Path ]) ->
	connect(Server),
	#response{ body = Body } = rpc:call(Server,webpage_file, get, [ Path ]),
	io:format("~s~n", [ Body ]);

process(Server,["file","list"]) ->
	connect(Server),
	Files = rpc:call(Server,webpage_file, list, []),
	io:format("~p~n", [ Files ]);

process(_Server,["file","help"]) ->
	io:format("
webpage [node] file [list|add|remove|get]

	list			returns a list of all file paths
	add Path Filename Mime	maps a path to local file with mimetype
	remove Path		removes a path mapping
	get Path		returns the contents of file at path mapping

");	


process(Server,["route","list"]) ->
	connect(Server),
	Res = rpc:call(Server,webpage_router,paths,[]),
	io:format("~p~n",[Res]);

process(Server,["route","add",Path,Args]) ->
	connect(Server),
	Bin = binary:list_to_bin(Args),
	io:format("Got json ~p~n", [ Bin ]),
	Terms = json:decode(Bin),
	io:format("Tems are ~p~n", [ Terms ]),
	rpc:call(Server,webpage_router,add, [ Path, Terms ]);

process(Server,["route","remove",Path]) ->
	connect(Server),
	rpc:call(Server,webpage_router,remove, [ Path ]);

process(Server,[ "route","get", Path ]) ->
	connect(Server),
	List = rpc:call(Server,webpage_router,route, [Path]),
	io:format("~p~n", [ List ]);

process(_Server,[ "route","help" ]) ->
	io:format("
webpage [node] route [list|add|remove|get]

	list			returns a list of paths
	add Path JSON		maps a path to a route list expressed in JSON
	remove Path		removes a route list for the given path 
	get Path		returns the route list associated with the given path

");

process(Server,["observer"]) ->
	connect(Server),
	rpc:call(Server,observer,start,[]);

process(Server,["console"]) ->
	connect(Server),
	process_flag(trap_exit,true),
	Shell = user_drv:start(['tty_sl -c -e',{Server,shell,start,[]}]),
	true = erlang:link(Shell),
	receive
		{ 'EXIT', Shell, _ } -> ok
	end,
	erlang:halt(0);

process(Server,["start"]) ->
	process_flag(trap_exit,true),
	case net_kernel:start([ Server, shortnames ]) of
		{ ok, Pid } ->
			io:format("Started ~p at ~p~n", [ Server, Pid ]),
			connect(Server),
			rpc:call(Server,mnesia,start,[]),
			rpc:call(Server,application,load,[webpage]),
			rpc:call(Server,webpage,start,[]);
		{ error, Reason } ->
			io:format("Failed to start ~p: ~p~n", [ Server, Reason ])	
	end,
	receive
		'EXIT' -> 
			io:format("done~n"),
			erlang:halt(0)
	end;

process(Server,["stop"]) ->
	connect(Server),
	rpc:call(Server, mnesia, stop, []),
	rpc:call(Server, webpage, stop, []),
	rpc:call(Server, erlang, halt, [0]);


process(Server,["user","add",User,Email,Password]) ->
	connect(Server),
	rpc:call(Server, webpage_auth, add, [ User, Email, Password ]);

process(Server,["user","auth", Path, User, Password]) ->
	connect(Server),
	Res = rpc:call(Server, webpage_auth, test, [ Path, User, Password ]),
	io:format("~p~n", [ Res ]);

process(Server,["user","remove",User,Email]) ->
	connect(Server),
	rpc:call(Server, webpage_auth, remove,[ User, Email ]);

process(Server,["user","grant", User, Pattern ]) ->
	connect(Server),
	rpc:call(Server, webpage_auth, grant, [ User, Pattern ]);	

process(Server,["user","revoke", User, Pattern ]) ->
	connect(Server),
	rpc:call(Server, webpage_auth, revoke, [ User, Pattern ]);

process(Server,["user","list"]) ->
	connect(Server),
	Users = rpc:call(Server, webpage_auth, users, []),
	io:format("~p~n", [ Users ]);

process(_Server,["user","help"]) ->
	io:format("
webpage user [list|add|remove|grant|revoke]

	list				returns a list of all the usernames in the system
	add User Email Password		creates a new user with email and password
	remove User Email		removes the user with matching name and password
	grant User Path			grants a access to a path for a given user
	revoke User Path		removes access to a path for a given user

");


%% This needs to be the last state in the process funciton, as it
%% is a catchall that prints out the status message.  If you are
%% adding new methods to it please add them above, and amend this
%% to reflect the new usage.
process(_Server,_) ->
	io:format("
usage: webpage [start|stop|status|console|observer|init|route|file|user]
	start [node]			- start a new node
	stop [node]			- stop a node
	status [node]			- return the status of a node
	console [node]			- connects a console to a node
	observer [node]			- run the observer on a node
	init [node] 			- initialize webpage database
	setup [node] file		- load a setup file on a node
	route [node] [list|add|remove|get]	
		list 			- list all routes by path
		add path module args	- add a MFA at a path
		remove path		- remove a MFA for path
		get path		- get the MFA for path
	file [node] [list|add|remove|get]
		list			- list all files by path
		add path file mime	- add a file at path with mime
		remove path		- remove a file at path
	user [node] [list|add|remove|grant|revoke]
		list			- list all users by name
		add user email pass	- add a user
		remove user email	- remove a user
		grant user path		- grant access to a path
		revoke user path	- remove access to a path
").

%% Loads a configuration file from our home base
%%
load_config(Home) ->
	case file:consult(Home ++ "/.webpage") of 
		{ok, Config } -> 
			Config;
		_ ->
			io:format("Please create a ~s/.webpage configuration file~n", [ Home ]), halt(0)
	end.

set_cookie(Config) ->
	case proplists:get_value(cookie,Config) of
		undefined ->
			ok;
		Cookie ->
			erlang:set_cookie(node(),Cookie)
	end.

setup_user(Server,User,Email,Password,Paths) ->
	rpc:call(Server, webpage_auth, add, [ User, Email, Password ]),
	[ rpc:call(Server, webpage_auth, grant, [ User, Path ]) ||
		Path <- Paths ].

find_server([]) ->
	none;
find_server([Host|_Args]) ->
	case string:tokens(Host,"@") of
		[ _Name , _Host ] -> Host;
		_ -> none
	end.

find_host(Config,[])->
	Host = proplists:get_value(host,Config),
	case Host of
		undefined ->
			io:format("Please set host in ~~/.botbop~n"), 
			halt(1);
		Host ->
			{ Host, [] }
	end;
find_host(Config,[ _A | As ] = Args) ->
	Server = find_server(Args),
	case { Server, proplists:get_value(host,Config) } of
		{ none, undefined } ->
			io:format("Please set host in ~~/.webpage~n"), 
			halt(1);
		{ Server, undefined } ->
			{ list_to_atom(Server), As };
		{ none, Host } ->
			{ Host, Args };
		{ Server, _Host } ->
			{ list_to_atom(Server), As }
	end.

main(Args) ->
	Home = os:getenv("HOME"),
	Config = load_config(Home),
	{ Host, Args2 } = find_host(Config,Args),
	ok = set_cookie(Config),
	io:format("Connecting to ~p~n", [ Host ]),
	process(Host,Args2).
